@echo off

REM # Automated setup script for Zephyr SDK distribution bundle archive
REM # for Windows host operating system

pushd %~dp0
setlocal ENABLEDELAYEDEXPANSION

REM # Read bundle version from file
set /p VERSION=<sdk_version

REM # Print banner
echo Zephyr SDK %VERSION% Setup
echo.

REM # Enable interactive mode if no argument is specified
if [%1] equ [] (
  set INTERACTIVE=y
  goto interactive
)

REM # Parse arguments if specified
:parsearg
if /i [%1] equ [/c] (
  set DO_CMAKE_PKG=y
) else if /i [%1] equ [/t] (
  set DO_HOSTTOOLS=y
) else (
  goto usage
)

shift
if [%1] neq [] (
  goto parsearg
)

goto process

:interactive
REM # Print interactive mode informational message
echo ** NOTE **
echo You only need to run this script once after extracting the Zephyr SDK
echo distribution bundle archive.
echo.

REM # Ask user for set-up choices
choice /c:yn /m:"Install Zephyr SDK CMake package"
if [%ERRORLEVEL%] equ [1] set DO_CMAKE_PKG=y

choice /c:yn /m:"Install host tools"
if [%ERRORLEVEL%] equ [1] set DO_HOSTTOOLS=y

echo.

:process
REM # Install Zephyr SDK CMake package
if [%DO_CMAKE_PKG%] neq [] (
  echo Installing Zephyr SDK CMake package ...
  cmake -P cmake\zephyr_sdk_export.cmake
  echo.
)

REM # Install host tools
if [%DO_HOSTTOOLS%] neq [] (
  echo Installing host tools ...
  echo SKIPPED: Windows host tools are not available yet.
  echo.
)

echo All done.
if [%INTERACTIVE%] neq [] (
  echo.
  echo Press any key to exit ...
  pause > nul
)

goto end

REM # Print scripting mode usage
:usage
echo %~n0 [/c] [/t]
echo.
echo   /c     Install Zephyr SDK CMake package
echo   /t     Install host tools
echo.
echo When no arguments are specified, the setup script runs in the interactive
echo mode asking for user inputs.

:end
endlocal
popd

exit /b 0
